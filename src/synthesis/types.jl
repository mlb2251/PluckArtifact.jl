using AutoHashEquals

export BaseType,
    Arrow,
    PType,
    parse_type,
    return_type,
    expansion_targets,
    getchildtype,
    arg_types,
    args_of_constructor,
    define_type!


export Constructor, SumProductType, Value, value, nat, list, constructors, snoclist
abstract type PType end


@auto_hash_equals struct Arrow <: PType
    arg_types::Vector{PType}
    return_type::PType

    Arrow(arg_types, return_type) = new(arg_types, return_type)
    # uncurry x -> (Y -> z) to (x,y) -> z
    Arrow(arg_types, return_type::Arrow) =
        new(vcat(arg_types, return_type.arg_types), return_type.return_type)
end

@auto_hash_equals struct BaseType <: PType
    name::Symbol
end

"""
Like (list (list int)) and such
"""
@auto_hash_equals struct TypeConstructor <: PType
    name::Symbol
    args::Vector{PType}
end




# mutable struct SumProductType
#     name::Symbol
#     constructors::Dict{Symbol, Vector{Symbol}}
# end
# Base.:(==)(x::SumProductType, y::SumProductType) = x.name === y.name

# const nat = SumProductType(:nat, Dict(:S => [:nat], :O => []))
# const list = SumProductType(:list, Dict(:Nil => [], :Cons => [:nat, :list]))
# const snoclist = SumProductType(:snoclist, Dict(:SNil => [], :Snoc => [:snoclist, :nat]))
# const bool = SumProductType(:bool, Dict(:True => [], :False => []))
# const unit = SumProductType(:unit, Dict(:Unit => []))

# const spt_of_constructor = Dict{Symbol, SumProductType}(
#     :Unit => unit,
#     :O => nat,
#     :S => nat,
#     :Nil => list,
#     :Cons => list,
#     :True => bool,
#     :False => bool,
#     :SNil => snoclist,
#     :Snoc => snoclist,
# )

# const type_of_spt = Dict{Symbol, PType}(
#     :nat => BaseType(:int),
#     :list => BaseType(:list),
#     :bool => BaseType(:bool),
#     :unit => BaseType(:unit),
#     :snoclist => BaseType(:snoclist),
#     :float => BaseType(:float),
# )

# function define_type!(name::Symbol, constructors::Dict{Symbol, Vector{Symbol}})
#     spt = SumProductType(name, constructors)
#     for constructor in keys(constructors)
#         haskey(spt_of_constructor, constructor) && @warn "redefinition: constructor $constructor already defined"
#         spt_of_constructor[constructor] = spt
#     end
#     haskey(type_of_spt, name) && @warn "redefinition: type $name already defined"
#     type_of_spt[name] = BaseType(name)
#     return spt
# end



num_args(::BaseType) = 0
num_args(t::Arrow) = length(t.arg_types)
num_args(t::TypeConstructor) = 0

return_type(t::BaseType) = t
return_type(t::Arrow) = t.return_type
return_type(t::TypeConstructor) = t

arg_types(::BaseType) = PType[]
arg_types(t::Arrow) = t.arg_types
arg_types(::TypeConstructor) = PType[]

Base.show(io::IO, t::BaseType) = print(io, t.name)
Base.show(io::IO, t::Arrow) =
    print(io, "(", join(t.arg_types, " -> "), " -> ", t.return_type, ")")
Base.show(io::IO, t::TypeConstructor) = print(io, "(", t.name, " ", join(t.args, " "), ")")

Base.parse(::Type{PType}, s::String) = parse_type(s)
parse_type(s::String) = parse_type(Pluck.tokenize(s))
parse_type(s::SubString{String}) = parse_type(Pluck.tokenize(s))
function parse_type(tokens)
    """
    Parse a type from a vector of tokens.
    int => BaseType(:int)
    ((int)) => BaseType(:int)
    foo => BaseType(:foo)
    x -> y -> z => Arrow([BaseType(:x), BaseType(:y)], BaseType(:z))
    # currying gets flattened away:
    x -> (Y -> z) => Arrow([BaseType(:x), BaseType(:y)], BaseType(:z))
    # however functions in an argument are of course preserved:
    (x -> y) -> z => Arrow([Arrow([BaseType(:x)], BaseType(:y))], BaseType(:z))
    """

    isempty(tokens) && error("unexpected end of input")
    types = PType[]
    connective = nothing
    while true
        token = tokens[1]
        if token == "("
            # parse the contents of these parens as a type
            end_idx = find_closeparn(tokens)
            push!(types, parse_type(tokens[2:end_idx-1]))
            tokens = tokens[end_idx+1:end]
        else
            # parse a base type
            @assert Meta.isidentifier(token) "expected a type name, got $(token)"
            push!(types, BaseType(Symbol(token)))
            tokens = tokens[2:end]
        end
        isempty(tokens) && break
        if tokens[1] == "->"
            @assert connective !== :constructor "unexpected mixture of arrows and type constructors - add more parentheses"
            connective = :arrow
            tokens = tokens[2:end]
        else
            @assert connective != :arrow "unexpected mixture of arrows and type constructors - add more parentheses"
            connective = :constructor
        end

    end

    # now we have a list of types, we can build the arrow type
    if connective === nothing
        @assert length(types) == 1
        return types[1]
    elseif connective === :arrow
        return Arrow(types[1:end-1], types[end])
    elseif connective === :constructor
        @assert types[1] isa BaseType
        return TypeConstructor(types[1].name, types[2:end])
    end
    error("unreachable")
end

function find_closeparn(tokens)::Int
    """
    Find the index of the matching ")" for the "(" at index 1.
    """
    @assert tokens[1] == "("
    depth = 0
    i = 1
    for (i, token) in enumerate(tokens)
        if token == "("
            depth += 1
        elseif token == ")"
            depth -= 1
            depth == 0 && return i
        end
    end
    error("unmatched parenthesis at start of $(join(tokens))")
end


const DEF_TYPES::Dict{Symbol, PType} = Dict{Symbol, PType}()
set_type!(name::Symbol, type::String) = (DEF_TYPES[name] = parse_type(type))
get_type(name::Symbol) = DEF_TYPES[name]

